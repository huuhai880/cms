SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[WA_WITHDRAWREQUEST_GetList_AdminWeb]
    @KEYWORD NVARCHAR(1000) = NULL,
    @STARTDATEREQUEST VARCHAR(20) = NULL,
    @ENDDATEREQUEST VARCHAR(20) = NULL,
    @STARTDATECONFIRM VARCHAR(20) = NULL,
    @ENDDATECONFIRM VARCHAR(20) = NULL,
    @STATUS INT = 0, --0: TAT CAT, 1: MOI, 2: THANH CONG, 3: HUY YEU CAU, 4: KHONG DUYET
    @PAGESIZE INT = 25,
	@PAGEINDEX INT = 1
AS
BEGIN
    
    DECLARE @FROMDATE_RQ DATETIME = NULL;
    DECLARE @TODATE_RQ DATETIME = NULL;

    DECLARE @FROMDATE_CF DATETIME = NULL;
    DECLARE @TODATE_CF DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL

    IF ISNULL(@STARTDATEREQUEST,'') <> '' SET @FROMDATE_RQ = TRY_CONVERT(DATETIME,@STARTDATEREQUEST,103)
	IF ISNULL(@ENDDATEREQUEST,'') <> '' SET @TODATE_RQ = TRY_CONVERT(DATETIME,@ENDDATEREQUEST,103)

    IF ISNULL(@STARTDATECONFIRM,'') <> '' SET @FROMDATE_CF = TRY_CONVERT(DATETIME,@STARTDATECONFIRM,103)
	IF ISNULL(@ENDDATECONFIRM,'') <> '' SET @TODATE_CF = TRY_CONVERT(DATETIME,@ENDDATECONFIRM,103)

    SELECT 
                WA_WITHDRAWREQUEST.WDREQUESTID,
                WA_WITHDRAWREQUEST.WDREQUESTNO,
                WA_WITHDRAWREQUEST.WDREQUESTSTATUS,
                WA_WITHDRAWREQUEST.WDVALUES,
                WA_WITHDRAWREQUEST.MEMBERID,
                CONCAT(CRM_ACCOUNT.CUSTOMERCODE, ' - ', CRM_ACCOUNT.FULLNAME) FULLNAME,
                FORMAT(WA_WITHDRAWREQUEST.WDDATEREQUEST, 'dd/MM/yyyy HH:mm:ss') WDDATEREQUEST,
                FORMAT(WA_WITHDRAWREQUEST.WDDATECONFIRM, 'dd/MM/yyyy HH:mm:ss') WDDATECONFIRM,
                WA_WITHDRAWREQUEST.CONFIRMUSER,
                IIF(WA_WITHDRAWREQUEST.CONFIRMUSER IS NOT NULL, SYS_USER.FULLNAME, '--') AS CONFIRMUSERFULLNAME,
                AFF_AFFILIATE.AFFILIATEID,
                COUNT(1) OVER() AS TOTALITEMS

    FROM        WA_WITHDRAWREQUEST

    JOIN        CRM_ACCOUNT
    ON          WA_WITHDRAWREQUEST.MEMBERID = CRM_ACCOUNT.MEMBERID
    AND         CRM_ACCOUNT.ISACTIVE = 1
    AND         CRM_ACCOUNT.ISDELETED = 0

    JOIN        AFF_AFFILIATE
    ON          AFF_AFFILIATE.MEMBERID = CRM_ACCOUNT.MEMBERID
    AND         AFF_AFFILIATE.ISACTIVE = 1
    AND         AFF_AFFILIATE.ISDELETED = 0

    LEFT JOIN   SYS_USER
    ON          SYS_USER.USERNAME = WA_WITHDRAWREQUEST.CONFIRMUSER
    AND         SYS_USER.ISACTIVE = 1
    AND         SYS_USER.ISDELETED = 0

    WHERE       WA_WITHDRAWREQUEST.ISACTIVE = 1
    AND         WA_WITHDRAWREQUEST.ISDELETED = 0
    AND         (
                    @KEYWORD IS NULL
                    OR (CRM_ACCOUNT.FULLNAME like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
                    OR (CRM_ACCOUNT.CUSTOMERCODE like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
                )
    AND         (@FROMDATE_RQ IS NULL OR WA_WITHDRAWREQUEST.WDDATEREQUEST >= @FROMDATE_RQ)
    AND         (@TODATE_RQ IS NULL OR WA_WITHDRAWREQUEST.WDDATEREQUEST < @TODATE_RQ + 1)
    AND         (@FROMDATE_CF IS NULL OR WA_WITHDRAWREQUEST.WDDATECONFIRM >= @FROMDATE_CF)
    AND         (@TODATE_CF IS NULL OR WA_WITHDRAWREQUEST.WDDATECONFIRM < @TODATE_CF + 1)
    AND         (
                    @STATUS = 0
                    OR WA_WITHDRAWREQUEST.WDREQUESTSTATUS = @STATUS
                )

    ORDER BY    WA_WITHDRAWREQUEST.CREATEDDATE DESC   
	OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY


END
GO
