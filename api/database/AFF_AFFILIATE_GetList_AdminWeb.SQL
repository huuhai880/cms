SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[AFF_AFFILIATE_GetList_AdminWeb]
    @KEYWORD NVARCHAR(500) = '',
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @AFFILIATETYPEID BIGINT = NULL,
    @ISACTIVE INT = 1,
    @PAGEINDEX INT = 1,
    @PAGESIZE INT = 25
AS
BEGIN

    DECLARE @FROMDATE DATETIME = NULL;
    DECLARE @TODATE DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL

    IF ISNULL(@STARTDATE,'') <> '' SET @FROMDATE = TRY_CONVERT(DATETIME,@STARTDATE,103)
	IF ISNULL(@ENDDATE,'') <> '' SET @TODATE = TRY_CONVERT(DATETIME,@ENDDATE,103)

    SELECT 
            AFF_AFFILIATE.AFFILIATEID,
            AFF_AFFILIATE.AFFILIATETYPEID,
            FORMAT(AFF_AFFILIATE.REGISTRATIONDATE, 'dd/MM/yyyy') REGISTRATIONDATEVIEW,
            AFF_AFFILIATE.MEMBERID,
            AFF_AFFILIATE.REGISTRATIONSOURCE,
            AFF_AFFILIATETYPE.AFFILIATETYPENAME,
            SL_ORDER.TOTALORDER,
            AFF_REFERRALINCOME.TOTALREFERRALVALUE AS TOTALCOMMISION,
            CRM_ACCOUNT.CUSTOMERCODE,
            CONCAT(CRM_ACCOUNT.CUSTOMERCODE,' - ', CRM_ACCOUNT.FULLNAME) FULLNAME,
            AFF_AFFILIATE.ISACTIVE,
            AFF_AFFILIATETYPE.ISAFFILIATELEVEL1,
            AFF_AFFILIATETYPE.ISAFFILIATELEVEL2,
            COUNT(1) OVER() AS TOTALITEMS
    FROM    AFF_AFFILIATE

    JOIN    CRM_ACCOUNT
    ON      CRM_ACCOUNT.MEMBERID = AFF_AFFILIATE.MEMBERID
    AND     CRM_ACCOUNT.ISACTIVE = 1
    AND     CRM_ACCOUNT.ISDELETED = 0

    JOIN    AFF_AFFILIATETYPE
    ON      AFF_AFFILIATETYPE.AFFILIATETYPEID = AFF_AFFILIATE.AFFILIATETYPEID
    AND     AFF_AFFILIATETYPE.ISACTIVE = 1
    AND     AFF_AFFILIATETYPE.ISDELETED = 0

    OUTER APPLY (
                    SELECT  COUNT(1) TOTALORDER
                    FROM    SL_ORDER
                    WHERE   SL_ORDER.ISACTIVE = 1
                    AND     SL_ORDER.REFERRALCODE = AFF_AFFILIATE.MEMBERID
                    AND     SL_ORDER.ISDELETED = 0
                    AND     SL_ORDER.[STATUS] = 1
                ) AS SL_ORDER
    OUTER APPLY (
                    SELECT 
                            SUM(AFF_REFERRALINCOME.TOTALREFERRALVALUE) AS TOTALREFERRALVALUE
                    FROM    AFF_REFERRALINCOME
                    WHERE   AFF_REFERRALINCOME.MEMBERID = AFF_AFFILIATE.MEMBERID
                    AND     AFF_REFERRALINCOME.ISACTIVE = 1
                ) AS AFF_REFERRALINCOME

    WHERE   AFF_AFFILIATE.ISDELETED = 0
    AND     (
                @KEYWORD IS NULL 
                OR @KEYWORD = ''
                OR CRM_ACCOUNT.CUSTOMERCODE like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                OR CRM_ACCOUNT.FULLNAME like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                OR CRM_ACCOUNT.PHONENUMBER like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
            )
    AND     (@ISACTIVE = 2 OR AFF_AFFILIATE.ISACTIVE = @ISACTIVE)
    AND     (@FROMDATE IS NULL OR AFF_AFFILIATE.REGISTRATIONDATE >= @FROMDATE)
    AND     (@TODATE IS NULL OR AFF_AFFILIATE.REGISTRATIONDATE < @TODATE + 1)
    AND     (@AFFILIATETYPEID IS NULL OR @AFFILIATETYPEID = 0 OR AFF_AFFILIATE.AFFILIATETYPEID = @AFFILIATETYPEID)

    ORDER BY    AFF_AFFILIATE.REGISTRATIONDATE DESC   
	OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY

END
GO
