SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[PRO_COMBOS_GetList_AdminWeb]
    @KEYWORD NVARCHAR(1000) = NULL,
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @ISACTIVE INT = 2, -- 1: ACTIVE, 0: NOT ACTIVE, 2 ALL
	@ISSHOWWEB INT = 2, 
    @PAGESIZE INT = 25,
	@PAGEINDEX INT = 1
AS
BEGIN
    DECLARE @FROMDATE DATETIME = NULL;
    DECLARE @TODATE DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL

    IF ISNULL(@STARTDATE,'') <> '' set @FROMDATE = try_convert(datetime,@STARTDATE,103)
	IF ISNULL(@ENDDATE,'') <> '' set @TODATE = try_convert(datetime,@ENDDATE,103)

    SELECT 
                PRO_COMBOS.COMBOID,
                PRO_COMBOS.COMBONAME,
                PRO_COMBOS.COMBOIMAGEURL,
                PRO_COMBOS.[DESCRIPTION],
								PRO_COMBOS.ISSHOWWEB,
                PRO_COMBOS.ISACTIVE,
                CONVERT(VARCHAR(20), PRO_COMBOS.CREATEDDATE, 103) CREATEDDATEVIEW,
                PRO_COMBOS.CREATEDUSER,
                SYS_USER.FULLNAME AS CREATEDUSERFULLNAME,
                IIF(TB_COMMENT.TOTAL > 0, 1, 0) AS ISNEWCOMMENT,
                COUNT(1) OVER() AS TOTALITEMS

    FROM        PRO_COMBOS
    LEFT JOIN   SYS_USER
    ON          PRO_COMBOS.CREATEDUSER =  SYS_USER.USERNAME
    AND         SYS_USER.ISACTIVE = 1
    AND         SYS_USER.ISDELETED = 0

    OUTER APPLY (
                    SELECT COUNT(1) AS TOTAL
                    FROM    PRO_COMMENT
                    WHERE   PRO_COMMENT.ISACTIVE = 1
                    AND     PRO_COMMENT.ISDELETED = 0
                    AND     PRO_COMMENT.COMBOID = PRO_COMBOS.COMBOID
                    AND     PRO_COMMENT.ISREVIEW IS NULL

                ) AS TB_COMMENT

    WHERE       PRO_COMBOS.ISDELETED = 0
    AND         (
                    @KEYWORD IS NULL 
                    OR (PRO_COMBOS.COMBONAME like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
                    OR (PRO_COMBOS.[DESCRIPTION] like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
		        )    
    AND         (@ISACTIVE = 2 OR PRO_COMBOS.ISACTIVE = @ISACTIVE)
	AND      (@ISSHOWWEB = 2 OR PRO_COMBOS.ISSHOWWEB = @ISSHOWWEB)
    AND         (@FROMDATE IS NULL OR PRO_COMBOS.CREATEDDATE >= @FROMDATE)
    AND         (@TODATE IS NULL OR PRO_COMBOS.CREATEDDATE < CAST((@TODATE + 1) AS DATE))

    ORDER BY    PRO_COMBOS.CREATEDDATE DESC   
	OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY

END
GO
