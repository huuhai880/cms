ALTER PROCEDURE CRM_ACCOUNT_Affiliate_GetList_AdminWeb
    @KEYWORD NVARCHAR(500) = NULL,
    @ISACTIVE INT = 2,
    @ISDELETED INT = 0,
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @AFFILIATETYPEID BIGINT = NULL,
    @POLICYCOMMISIONID BIGINT = 0,
    @STATUS INT = 2,
    @PAGEINDEX INT = 1,
    @PAGESIZE INT = 25 
AS 
BEGIN

    DECLARE @FROMDATE DATETIME = NULL;
    DECLARE @TODATE DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL

    IF ISNULL(@STARTDATE,'') <> '' SET @FROMDATE = TRY_CONVERT(DATETIME,@STARTDATE,103)
	IF ISNULL(@ENDDATE,'') <> '' SET @TODATE = TRY_CONVERT(DATETIME,@ENDDATE,103)

    SELECT 
                CRM_ACCOUNT.MEMBERID,
                CRM_ACCOUNT.CUSTOMERCODE,
                CRM_ACCOUNT.FULLNAME,
                '' POLICYCOMMISIONNAME,
                0 TOTALORDER,
                0 TOTALCOMMISION,
                REGISTRATIONSOURCE,
                CRM_ACCOUNT.STATUSAFFILIATE,
                FORMAT(CRM_ACCOUNT.REGISTRATIONDATE, 'dd/MM/yyyy HH:mm:ss') REGISTRATIONDATEVIEW,
                CRM_ACCOUNT.AFFILIATETYPEID,
                AFF_AFFILIATETYPE.AFFILIATETYPENAME,
                CRM_ACCOUNT.ISACTIVE,
                COUNT(1) OVER() AS TOTALITEMS

    FROM        CRM_ACCOUNT

    JOIN        AFF_AFFILIATETYPE
    ON          AFF_AFFILIATETYPE.AFFILIATETYPEID = CRM_ACCOUNT.AFFILIATETYPEID
    AND         AFF_AFFILIATETYPE.ISACTIVE = 1
    AND         AFF_AFFILIATETYPE.ISDELETED = 0

    WHERE       CRM_ACCOUNT.REGISTRATIONDATE IS NOT NULL
    AND         CRM_ACCOUNT.STATUSAFFILIATE IS NOT NULL
    AND         (
                    @KEYWORD IS NULL 
                    OR @KEYWORD = ''
                    OR CRM_ACCOUNT.CUSTOMERCODE like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                    OR CRM_ACCOUNT.FULLNAME like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                    OR CRM_ACCOUNT.PHONENUMBER like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                )
    AND         (@ISACTIVE = 2 OR CRM_ACCOUNT.ISACTIVE = @ISACTIVE)
    AND         (@ISDELETED = 2 OR CRM_ACCOUNT.ISDELETED = @ISDELETED)
     
    AND         (@FROMDATE IS NULL OR CRM_ACCOUNT.REGISTRATIONDATE >= @FROMDATE)
    AND         (@TODATE IS NULL OR CRM_ACCOUNT.REGISTRATIONDATE < @TODATE + 1)

    AND         (
                    @AFFILIATETYPEID IS NULL
                    OR @AFFILIATETYPEID = 0
                    OR CRM_ACCOUNT.AFFILIATETYPEID = @AFFILIATETYPEID
                )

    AND         (
                    @STATUS IS NULL 
                    OR @STATUS = 0
                    OR CRM_ACCOUNT.STATUSAFFILIATE = @STATUS
                )

    ORDER BY    CRM_ACCOUNT.REGISTRATIONDATE DESC   
	OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY

    SELECT  COUNT(1) AS TOTALITEMS
    FROM    CRM_ACCOUNT
    WHERE   CRM_ACCOUNT.ISACTIVE = 1
    AND     CRM_ACCOUNT.ISDELETED = 0
    AND     CRM_ACCOUNT.STATUSAFFILIATE = 1

END