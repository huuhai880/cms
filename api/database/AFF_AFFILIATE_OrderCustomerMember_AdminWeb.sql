ALTER PROCEDURE AFF_AFFILIATE_OrderCustomerMember_AdminWeb
    @MEMBERID BIGINT = 0,
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @TYPE VARCHAR(30) = 'order' ,
    @PAGEINDEX INT = 1,
    @PAGESIZE INT = 25
AS 
BEGIN

    DECLARE @FROMDATE AS DATETIME = NULL,
            @TODATE AS DATETIME = NULL;

    IF(ISNULL(@STARTDATE,'') <> '') SET @FROMDATE = TRY_CONVERT(DATETIME, @STARTDATE, 103);
    IF(ISNULL(@ENDDATE,'') <> '') SET @TODATE = TRY_CONVERT(DATETIME, @ENDDATE, 103);


    IF(@TYPE = 'order')
    BEGIN
        SELECT      
                    FORMAT(SL_ORDER.ORDERDATE, 'dd/MM/yyyy HH:mm:ss') ORDERDATE,
                    SL_ORDER.ORDERNO,
                    SL_ORDER.ORDERID,
                    SL_ORDER.TOTALMONEY, --GIA TRI DON HANG
                    SL_ORDER.MEMBERID,
                    CRM_ACCOUNT.CUSTOMERCODE,
                    CONCAT(CRM_ACCOUNT.CUSTOMERCODE, ' - ', CRM_ACCOUNT.FULLNAME) FULLNAME,
                    AFF_REFINCOMEDETAIL.REFERRALVALUE, --HOA HONG
                    IIF(SL_ORDER.[STATUS] = 1, N'Hoàn thành', N'Chưa thanh toán') AS STATUS,
                    COUNT(1) OVER() AS TOTALITEMS

        FROM        SL_ORDER

        JOIN        CRM_ACCOUNT
        ON          CRM_ACCOUNT.MEMBERID = SL_ORDER.MEMBERID
        AND         CRM_ACCOUNT.ISDELETED = 0
        AND         CRM_ACCOUNT.ISACTIVE = 1

        LEFT JOIN   AFF_REFINCOMEDETAIL
        ON          AFF_REFINCOMEDETAIL.ORDERID = SL_ORDER.ORDERID

        WHERE       SL_ORDER.ISACTIVE = 1
        AND         SL_ORDER.ISDELETED = 0
        AND         SL_ORDER.REFERRALCODE = @MEMBERID
        AND         (@FROMDATE IS NULL OR SL_ORDER.ORDERDATE >= @FROMDATE)
        AND         (@TODATE IS NULL OR SL_ORDER.ORDERDATE < @TODATE + 1)

        ORDER BY    SL_ORDER.ORDERDATE DESC 
        OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	    FETCH NEXT  @PAGESIZE ROWS ONLY
    END
    ELSE IF(@TYPE = 'customer')
    BEGIN
        SELECT      
                    CRM_ACCOUNT.MEMBERID,
                    CRM_ACCOUNT.CUSTOMERCODE,
                    CONCAT(CRM_ACCOUNT.CUSTOMERCODE, ' - ', CRM_ACCOUNT.FULLNAME) FULLNAME,
                    CRM_ACCOUNT.EMAIL,
                    CRM_ACCOUNT.PHONENUMBER,
                    FORMAT(CRM_ACCOUNT.CREATEDDATE, 'dd/MM/yyyy') CREATEDDATEVIEW,
                    COUNT(1) OVER() AS TOTALITEMS
        FROM        CRM_ACCOUNT
        WHERE       CRM_ACCOUNT.ISACTIVE = 1
        AND         CRM_ACCOUNT.ISDELETED = 0
        AND         CRM_ACCOUNT.REFERRALCODE = @MEMBERID
        AND         (@FROMDATE IS NULL OR CRM_ACCOUNT.CREATEDDATE >= @FROMDATE)
        AND         (@TODATE IS NULL OR CRM_ACCOUNT.CREATEDDATE < @TODATE + 1)

        ORDER BY    CREATEDDATE DESC 
        OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	    FETCH NEXT  @PAGESIZE ROWS ONLY
    END
    ELSE IF(@TYPE = 'member')
    BEGIN
        SELECT 
                    AFF_RELATIONSHIPS.MEMBERID, -- THANH VIEN
                    FORMAT(AFF_AFFILIATE.APPROVEDDATE,'dd/MM/yyyy HH:mm:ss') APPROVEDDATEVIEW,
                    CONCAT(CRM_ACCOUNT.CUSTOMERCODE, ' - ', CRM_ACCOUNT.FULLNAME) FULLNAME,
                    AFF_REFERRALINCOME.TOTALORDERVALUE,
                    AFF_REFERRALINCOME.TOTALREFERRALVALUE,
                    COUNT(1) OVER() AS TOTALITEMS
        FROM        AFF_RELATIONSHIPS

        JOIN        CRM_ACCOUNT
        ON          CRM_ACCOUNT.MEMBERID = AFF_RELATIONSHIPS.MEMBERID
        AND         CRM_ACCOUNT.ISDELETED = 0
        AND         CRM_ACCOUNT.ISACTIVE = 1

        JOIN        AFF_AFFILIATE
        ON          AFF_AFFILIATE.MEMBERID = AFF_RELATIONSHIPS.MEMBERID

        OUTER APPLY (
                        SELECT 
                                SUM(TOTALORDERVALUE) TOTALORDERVALUE,
                                SUM(TOTALREFERRALVALUE) TOTALREFERRALVALUE
                        FROM    AFF_REFERRALINCOME
                        WHERE   AFF_REFERRALINCOME.MEMBERID = AFF_RELATIONSHIPS.MEMBERID
                        AND     AFF_REFERRALINCOME.ISACTIVE = 0
                        AND     (@FROMDATE IS NULL OR AFF_REFERRALINCOME.CREATEDDATE >= @FROMDATE)
                        AND     (@TODATE IS NULL OR AFF_REFERRALINCOME.CREATEDDATE < @TODATE + 1)
                    ) AS AFF_REFERRALINCOME

        WHERE       AFF_RELATIONSHIPS.REFMEMBERID = @MEMBERID --LEADER
        AND         AFF_RELATIONSHIPS.[LEVEL] = 2
        AND         (@FROMDATE IS NULL OR AFF_AFFILIATE.APPROVEDDATE >= @FROMDATE)
        AND         (@TODATE IS NULL OR AFF_AFFILIATE.APPROVEDDATE < @TODATE + 1)
        ORDER BY    AFF_AFFILIATE.APPROVEDDATE DESC 
        OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	    FETCH NEXT  @PAGESIZE ROWS ONLY
    END

END