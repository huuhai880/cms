SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[AFF_RELATIONSHIPS_Create_AdminWeb]
    @AFFILIATETYPEID BIGINT = 0,
    @MEMBERID BIGINT = 0, --AFFILIATE DANG TAO HOAC CAP NHAT
    @AFFILIATELEADERID BIGINT = 0 --AFFILIATE REF NEU CHON LEADER
AS 
BEGIN

    DECLARE @ISAFFILIATELEVEL2 AS BIT = 0;
    SELECT TOP 1 @ISAFFILIATELEVEL2 = ISAFFILIATELEVEL2
    FROM    AFF_AFFILIATETYPE
    WHERE   AFF_AFFILIATETYPE.ISACTIVE = 1
    AND     AFF_AFFILIATETYPE.ISDELETED = 0 
    AND     AFF_AFFILIATETYPE.AFFILIATETYPEID = @AFFILIATETYPEID

    --NEU LA LEADER 
    IF(@ISAFFILIATELEVEL2 = 1)
    BEGIN
        --KIEM TRA DA TON TAI HAY CHUA NEU CHUA THI INSERT
        IF(NOT EXISTS (SELECT 1 FROM AFF_RELATIONSHIPS WHERE MEMBERID = @MEMBERID AND  REFMEMBERID = 0 AND [LEVEL] = 1))
        BEGIN
            INSERT INTO AFF_RELATIONSHIPS
            (
                MEMBERID,
                REFMEMBERID,
                [LEVEL],
                CREATEDDATE
            )
            VALUES
            (
                @MEMBERID,
                0,
                1,
                GETDATE()
            )
        END

        --KIEM TRA NEU CO THUOC LEADER NAO THI XOA
        --VI DA LEN LEADER
        IF(EXISTS (SELECT 1 FROM AFF_RELATIONSHIPS WHERE MEMBERID = @MEMBERID AND [LEVEL] = 2))
        BEGIN
            DELETE  AFF_RELATIONSHIPS
            WHERE   MEMBERID = @MEMBERID
            AND     [LEVEL] = 2
        END
    END
    ELSE -- NEU LA MEMBER
    BEGIN
        --KIEM TRA NEU LA LEADER MA CHUYEN THANH MEMBER THI XOA HET MOI QUAN HE
        IF(EXISTS (SELECT 1 FROM AFF_RELATIONSHIPS WHERE MEMBERID = @MEMBERID AND REFMEMBERID = 0 AND [LEVEL] = 1))
        BEGIN
            --XOA LEADER
            DELETE AFF_RELATIONSHIPS
            WHERE   MEMBERID = @MEMBERID
            AND     REFMEMBERID = 0
            AND     [LEVEL] = 1;

            --XOA CAC THANH VIEN
            DELETE AFF_RELATIONSHIPS
            WHERE   REFMEMBERID = @MEMBERID
            AND     [LEVEL] = 2
        END

        --THEM MOI THANH VIEN NEU CO CHON LEADER
        IF(@AFFILIATELEADERID IS NOT NULL AND @AFFILIATELEADERID <> 0)
        BEGIN
            INSERT INTO AFF_RELATIONSHIPS
            (
                MEMBERID,
                REFMEMBERID, 
                [LEVEL],
                CREATEDDATE
            )
            VALUES
            (
                @MEMBERID,
                @AFFILIATELEADERID,
                2,
                GETDATE()
            )
        END
    END
END
GO
