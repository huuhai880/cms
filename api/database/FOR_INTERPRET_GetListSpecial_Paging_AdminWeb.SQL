SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[FOR_INTERPRET_GetListSpecial_Paging_AdminWeb]
    @KEYWORD NVARCHAR(500) = NULL,
    @ISCONDITIONOR INT = 2, --TAT CA: 2, HOAC : 1 VA 0
    @PAGESIZE INT = 25,
    @PAGEINDEX INT = 1
AS
BEGIN

    IF(@KEYWORD = '') SET @KEYWORD = NULL

    CREATE TABLE #TB_INTERPRET (
        INTERPRETID BIGINT,
        BRIEFDESCRIPTION NVARCHAR(MAX),
        DESCRIPTION NVARCHAR(MAX),
        ISCONDITIONOR BIT,
        ISINTERPRETSPECIAL BIT,
        ORDERINDEX INT,
        STT INT,
        TOTALITEMS INT
    )

    INSERT INTO #TB_INTERPRET
    SELECT 
            INTERPRETID,
            BRIEFDESCRIPTION,
            [DESCRIPTION],
            ISCONDITIONOR,
            ISINTERPRETSPECIAL,
            ORDERINDEX,
            ROW_NUMBER() OVER (ORDER BY FOR_INTERPRET.CREATEDDATE DESC) AS STT , 
            COUNT(1) OVER() AS TOTALITEMS
    FROM    FOR_INTERPRET
    WHERE   FOR_INTERPRET.ISACTIVE = 1
    AND     FOR_INTERPRET.ISDELETED = 0
    AND     FOR_INTERPRET.ISINTERPRETSPECIAL = 1

    AND     (
                @ISCONDITIONOR = 2 --TAT CA
                OR (@ISCONDITIONOR = 1 --HOAC
                    AND FOR_INTERPRET.ISCONDITIONOR = 1)
                OR (@ISCONDITIONOR = 0 --VA
                    AND FOR_INTERPRET.ISCONDITIONOR = 0)
            )
    AND     (
                @KEYWORD IS NULL 
                OR @KEYWORD = ''
                OR FOR_INTERPRET.BRIEFDESCRIPTION LIKE '%' + LTRIM(RTRIM(@KEYWORD))+'%' collate Latin1_General_CI_AI_WS
                OR EXISTS 
                        (
                            SELECT  1
                            FROM    FOR_INTERPRETDETAIL
                            WHERE   FOR_INTERPRETDETAIL.ISACTIVE = 1
                            AND     FOR_INTERPRETDETAIL.INTERPRETID = FOR_INTERPRET.INTERPRETID
                            AND     FOR_INTERPRETDETAIL.ISDELETED = 0
                            AND     FOR_INTERPRETDETAIL.INTERPRETDETAILNAME LIKE '%' + LTRIM(RTRIM(@KEYWORD))+'%' collate Latin1_General_CI_AI_WS 
                        )
                OR EXISTS 
                        (
                            SELECT 1
                            FROM    FOR_INTERPRET_ATTRIBUTE

                            JOIN    FOR_ATTRIBUTES
                            ON      FOR_ATTRIBUTES.ATTRIBUTEID = FOR_INTERPRET_ATTRIBUTE.ATTRIBUTEID
                            AND     FOR_ATTRIBUTES.ISACTIVE = 1
                            AND     FOR_ATTRIBUTES.ISDELETED = 0

                            WHERE   FOR_INTERPRET_ATTRIBUTE.ISACTIVE = 1
                            AND     FOR_INTERPRET_ATTRIBUTE.ISDELETED = 0
                            AND     FOR_INTERPRET_ATTRIBUTE.INTERPRETID = FOR_INTERPRET.INTERPRETID
                            AND     FOR_ATTRIBUTES.ATTRIBUTENAME LIKE '%' + LTRIM(RTRIM(@KEYWORD))+'%' collate Latin1_General_CI_AI_WS      
                        )
            )

    ORDER BY    FOR_INTERPRET.CREATEDDATE DESC
    OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY;

    --LAY DANH SACH LUAN GIAI DAC BIET
    SELECT *
    FROM    #TB_INTERPRET;


    --LAY DANH SACH LUAN GIAI CON
    SELECT 
            FOR_INTERPRETDETAIL.INTERPRETID,
            FOR_INTERPRETDETAIL.INTERPRETDETAILID,
            FOR_INTERPRETDETAIL.INTERPRETDETAILNAME,
            FOR_INTERPRETDETAIL.FULLCONTENT,
            FOR_INTERPRETDETAIL.SHORTCONTENT,
            FOR_INTERPRETDETAIL.ORDERINDEX
    FROM    FOR_INTERPRETDETAIL
    WHERE   FOR_INTERPRETDETAIL.ISACTIVE = 1
    AND     FOR_INTERPRETDETAIL.ISDELETED = 0
    AND     EXISTS (
                        SELECT  1 
                        FROM    #TB_INTERPRET 
                        WHERE   FOR_INTERPRETDETAIL.INTERPRETID = #TB_INTERPRET.INTERPRETID
                    )

    --LAY DANH SACH THUOC TINH
    SELECT 
            FOR_INTERPRET_ATTRIBUTE.INTERPRETID,
            FOR_INTERPRET_ATTRIBUTE.INTERPRETATTRIBUTEID,
            FOR_INTERPRET_ATTRIBUTE.ATTRIBUTEID,
            FOR_INTERPRET_ATTRIBUTE.MAINNUMBER,
            FOR_INTERPRET_ATTRIBUTE.MAINNUMBERID,
            FOR_ATTRIBUTES.ATTRIBUTENAME

    FROM    FOR_INTERPRET_ATTRIBUTE

    JOIN    FOR_ATTRIBUTES
    ON      FOR_ATTRIBUTES.ATTRIBUTEID = FOR_INTERPRET_ATTRIBUTE.ATTRIBUTEID
    AND     FOR_ATTRIBUTES.ISACTIVE = 1
    AND     FOR_ATTRIBUTES.ISDELETED = 0

    WHERE   FOR_INTERPRET_ATTRIBUTE.ISDELETED = 0
    AND     ISNULL(FOR_INTERPRET_ATTRIBUTE.ISACTIVE, 1) = 1
    AND     EXISTS (
                        SELECT  1 
                        FROM    #TB_INTERPRET 
                        WHERE   FOR_INTERPRET_ATTRIBUTE.INTERPRETID = #TB_INTERPRET.INTERPRETID
                    )

    DROP TABLE #TB_INTERPRET

END
GO
