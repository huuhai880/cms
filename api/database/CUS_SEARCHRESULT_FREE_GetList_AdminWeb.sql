ALTER PROCEDURE CUS_SEARCHRESULT_FREE_GetList_AdminWeb
    @KEYWORD NVARCHAR(1000) = NULL,
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @PRODUCTID BIGINT = NULL,
    @PAGESIZE INT = 25,
	@PAGEINDEX INT = 1
AS
BEGIN

    DECLARE @FROMDATE DATETIME = NULL;
    DECLARE @TODATE DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL

    IF ISNULL(@STARTDATE,'') <> '' SET @FROMDATE = TRY_CONVERT(DATETIME,@STARTDATE,103)
	IF ISNULL(@ENDDATE,'') <> '' SET @TODATE = TRY_CONVERT(DATETIME,@ENDDATE,103)

    SELECT 
                CUS_SEARCHRESULT_FREE.SEARCHRESULTFREEID,
                CUS_SEARCHRESULT_FREE.FULLNAME,
                CUS_SEARCHRESULT_FREE.BIRTHDAY,
                CUS_SEARCHRESULT_FREE.GENDER,
                FORMAT(CUS_SEARCHRESULT_FREE.SEARCHDATE, 'dd/MM/yyyy HH:mm:ss') AS SEARCHDATEVIEW,
                CUS_SEARCHRESULT_FREE.PRODUCTID,
                MD_PRODUCT.PRODUCTNAME,
                COUNT(1) OVER() AS TOTALITEMS

    FROM        CUS_SEARCHRESULT_FREE

    LEFT JOIN   MD_PRODUCT
    ON          MD_PRODUCT.PRODUCTID = CUS_SEARCHRESULT_FREE.PRODUCTID
    AND         MD_PRODUCT.ISDELETED = 0
    AND         MD_PRODUCT.ISACTIVE = 1

    WHERE       CUS_SEARCHRESULT_FREE.ISDELETED = 0
    AND         (
                    @KEYWORD IS NULL
                    OR @KEYWORD = ''
                    OR CUS_SEARCHRESULT_FREE.FULLNAME LIKE '%' + @KEYWORD + '%'  collate Latin1_General_CI_AI_WS
                )
    AND         (@PRODUCTID IS NULL OR CUS_SEARCHRESULT_FREE.PRODUCTID = @PRODUCTID)

    AND         (@FROMDATE IS NULL OR CUS_SEARCHRESULT_FREE.SEARCHDATE >= @FROMDATE)
    AND         (@TODATE IS NULL OR CUS_SEARCHRESULT_FREE.SEARCHDATE < @TODATE + 1)

    ORDER BY    CUS_SEARCHRESULT_FREE.SEARCHDATE DESC
    OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY


END