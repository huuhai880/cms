ALTER PROCEDURE AFF_AFFILIATEREQUEST_GetList_AdminWeb
    @KEYWORD NVARCHAR(200) = '',
    @FROMDATEREGISTER VARCHAR(20) = NULL,
    @TODATEREGISTER VARCHAR(20) = NULL,
    @FROMDATEAPPROVE VARCHAR(20) = NULL,
    @TODATEAPPROVE VARCHAR(20) = NULL,
    @STATUS INT = 1,
    @PAGEINDEX INT = 1,
    @PAGESIZE INT = 25
AS
BEGIN

    DECLARE @FROMDATEREGISTER_D AS DATETIME = NULL;
    DECLARE @TODATEREGISTER_D AS DATETIME = NULL;
    DECLARE @FROMDATEAPPROVE_D AS DATETIME = NULL;
    DECLARE @TODATEAPPROVE_D AS DATETIME = NULL;

    IF ISNULL(@FROMDATEREGISTER,'') <> '' SET @FROMDATEREGISTER_D = TRY_CONVERT(DATETIME,@FROMDATEREGISTER,103)
	IF ISNULL(@TODATEREGISTER,'') <> '' SET @TODATEREGISTER_D = TRY_CONVERT(DATETIME,@TODATEREGISTER,103)

    IF ISNULL(@FROMDATEAPPROVE,'') <> '' SET @FROMDATEAPPROVE_D = TRY_CONVERT(DATETIME,@FROMDATEAPPROVE,103)
	IF ISNULL(@TODATEAPPROVE,'') <> '' SET @TODATEAPPROVE_D = TRY_CONVERT(DATETIME,@TODATEAPPROVE,103)


    SELECT 
                AFF_AFFILIATEREQUEST.AFFILIATEREQUESTID,
                AFF_AFFILIATEREQUEST.REQUESTNO,
                AFF_AFFILIATEREQUEST.MEMBERID,
                AFF_AFFILIATEREQUEST.REQUESTSTATUS,
                AFF_AFFILIATEREQUEST.ISAGREE,
                FORMAT(AFF_AFFILIATEREQUEST.REGISTRATIONDATE, 'dd/MM/yyyy HH:mm:ss') REGISTRATIONDATEVIEW,
                IIF(AFF_AFFILIATEREQUEST.APPROVEDDATE IS NOT NULL, FORMAT(AFF_AFFILIATEREQUEST.APPROVEDDATE, 'dd/MM/yyyy HH:mm:ss'), '') APPROVEDDATEVIEW,
                AFF_AFFILIATEREQUEST.REVIEWUSER,
                AFF_AFFILIATEREQUEST.REVIEWNOTE,
                CRM_ACCOUNT.CUSTOMERCODE,
                CONCAT(CRM_ACCOUNT.CUSTOMERCODE, '-', CRM_ACCOUNT.FULLNAME) FULLNAME,
                IIF(AFF_AFFILIATEREQUEST.REVIEWUSER IS NOT NULL, 
                CONCAT(SYS_USER.USERNAME,'-', SYS_USER.FULLNAME), '') REVIEWUSERFULLNAME,
                COUNT(1) OVER()AS TOTALITEMS

    FROM        AFF_AFFILIATEREQUEST

    JOIN        CRM_ACCOUNT
    ON          CRM_ACCOUNT.MEMBERID = AFF_AFFILIATEREQUEST.MEMBERID
    AND         CRM_ACCOUNT.ISACTIVE = 1
    AND         CRM_ACCOUNT.ISDELETED = 0

    LEFT JOIN   SYS_USER
    ON          SYS_USER.USERNAME = AFF_AFFILIATEREQUEST.REVIEWUSER
    AND         SYS_USER.ISDELETED = 0
    AND         SYS_USER.ISACTIVE = 1

    WHERE       AFF_AFFILIATEREQUEST.ISDELETED = 0
    AND         (
                    @KEYWORD IS NULL
                    OR @KEYWORD = ''
                    OR CRM_ACCOUNT.CUSTOMERCODE like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                    OR CRM_ACCOUNT.FULLNAME like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                    OR CRM_ACCOUNT.PHONENUMBER like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS
                )
    AND         (
                    @STATUS = 0
                    OR AFF_AFFILIATEREQUEST.REQUESTSTATUS = @STATUS
                )
    AND         (@FROMDATEREGISTER_D IS NULL OR AFF_AFFILIATEREQUEST.REGISTRATIONDATE >= @FROMDATEREGISTER_D)
    AND         (@TODATEREGISTER_D IS NULL OR AFF_AFFILIATEREQUEST.REGISTRATIONDATE < @TODATEREGISTER_D + 1)

    AND         (@FROMDATEAPPROVE_D IS NULL OR AFF_AFFILIATEREQUEST.APPROVEDDATE >= @FROMDATEAPPROVE_D)
    AND         (@TODATEAPPROVE_D IS NULL OR AFF_AFFILIATEREQUEST.APPROVEDDATE < @TODATEAPPROVE_D + 1)

    ORDER BY    AFF_AFFILIATEREQUEST.REGISTRATIONDATE DESC
    OFFSET (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT @PAGESIZE ROWS ONLY  

END