SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[MD_PRODUCT_GetList_AdminWeb]
    @KEYWORD NVARCHAR(1000) = NULL,
    @PRODUCTCATEGORYID BIGINT = 0,
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @ISACTIVE INT = 2, -- 1: ACTIVE, 0: NOT ACTIVE, 2 ALL
	@ISSHOWWEB INT = 2, 
    @PAGESIZE INT = 25,
	@PAGEINDEX INT = 1
AS
BEGIN

    DECLARE @FROMDATE DATETIME = NULL;
    DECLARE @TODATE DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL
	IF @PRODUCTCATEGORYID = 0 SET @PRODUCTCATEGORYID = NULL

    IF ISNULL(@STARTDATE,'') <> '' set @FROMDATE = try_convert(datetime,@STARTDATE,103)
	IF ISNULL(@ENDDATE,'') <> '' set @TODATE = try_convert(datetime,@ENDDATE,103)


    SELECT 
                MD_PRODUCT.PRODUCTID,
                MD_PRODUCT.PRODUCTNAME,
                MD_PRODUCT.PRODUCTNAMESHOWWEB,
                MD_PRODUCT.ISSHOWWEB,
                MD_PRODUCT.ISACTIVE,
                MD_PRODUCT.PRODUCTCATEGORYID,
                MD_PRODUCTCATEGORY.CATEGORYNAME,
                0 PRICE,
                ISNULL(ISWEBVIEW, 0) ISWEBVIEW,
                ISNULL(ISSHOWMENU, 0) ISSHOWMENU,
                IIF(TB_COMMENT.TOTAL > 0, 1, 0) AS ISNEWCOMMENT,
                COUNT(1) OVER() AS TOTALITEMS
    FROM        MD_PRODUCT
    JOIN        MD_PRODUCTCATEGORY
    ON          MD_PRODUCT.PRODUCTCATEGORYID = MD_PRODUCTCATEGORY.PRODUCTCATEGORYID
    AND         MD_PRODUCTCATEGORY.ISACTIVE = 1
    AND         MD_PRODUCTCATEGORY.ISDELETED = 0

    OUTER APPLY (
                    SELECT COUNT(1) AS TOTAL
                    FROM    PRO_COMMENT
                    WHERE   PRO_COMMENT.ISACTIVE = 1
                    AND     PRO_COMMENT.ISDELETED = 0
                    AND     PRO_COMMENT.PRODUCTID = MD_PRODUCT.PRODUCTID
                    AND     PRO_COMMENT.ISREVIEW IS NULL

                ) AS TB_COMMENT

    WHERE       MD_PRODUCT.ISDELETED = 0
    AND         (
                    @KEYWORD IS NULL OR ( MD_PRODUCT.PRODUCTNAME like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
	 				OR ( MD_PRODUCT.PRODUCTCODE like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
                    OR ( MD_PRODUCT.PRODUCTNAMESHOWWEB like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
				)
    AND         (@ISACTIVE = 2 OR MD_PRODUCT.ISACTIVE = @ISACTIVE)
	AND         (@ISSHOWWEB = 2 OR MD_PRODUCT.ISSHOWWEB = @ISSHOWWEB)
    AND         (@PRODUCTCATEGORYID IS NULL OR MD_PRODUCT.PRODUCTCATEGORYID = @PRODUCTCATEGORYID)
    AND         (@FROMDATE IS NULL OR MD_PRODUCT.CREATEDDATE >= @FROMDATE)
    AND         (@TODATE IS NULL OR MD_PRODUCT.CREATEDDATE < CAST((@TODATE + 1) AS DATE))

    ORDER BY    MD_PRODUCT.CREATEDDATE DESC   
	OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY

END
GO
