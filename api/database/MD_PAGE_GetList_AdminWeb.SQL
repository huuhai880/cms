SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[MD_PAGE_GetList_AdminWeb]
    @KEYWORD NVARCHAR(1000) = NULL,
    @STARTDATE VARCHAR(20) = NULL,
    @ENDDATE VARCHAR(20) = NULL,
    @ISACTIVE INT = 2, -- 1: ACTIVE, 0: NOT ACTIVE, 2 ALL
    @PAGESIZE INT = 25,
	@PAGEINDEX INT = 1
AS
BEGIN

    DECLARE @FROMDATE DATETIME = NULL;
    DECLARE @TODATE DATETIME = NULL;

    IF @KEYWORD ='' SET @KEYWORD = NULL

    IF ISNULL(@STARTDATE,'') <> '' SET @FROMDATE = TRY_CONVERT(DATETIME,@STARTDATE,103)
	IF ISNULL(@ENDDATE,'') <> '' SET @TODATE = TRY_CONVERT(DATETIME,@ENDDATE,103)

    SELECT 
                MD_PAGE.PAGEID,
                MD_PAGE.PAGENAME,
                MD_PAGE.PAGETYPE,
                MD_PAGE.TITLEPAGE,
                MD_PAGE.SHORTDESCRIPTION,
                MD_PAGE.BACKGROUND,
                MD_PAGE.BACKGROUNDURL,
                MD_PAGE.ISACTIVE,
                FORMAT(MD_PAGE.CREATEDDATE, 'dd/MM/yyyy') CREATEDDATEVIEW,
                MD_PAGE.CREATEDUSER,
                SYS_USER.FULLNAME,
                COUNT(1) OVER() AS TOTALITEMS

    FROM        MD_PAGE

    LEFT JOIN   SYS_USER
    ON          SYS_USER.USERNAME = MD_PAGE.CREATEDUSER
    AND         SYS_USER.ISACTIVE = 1
    AND         SYS_USER.ISDELETED = 0

    WHERE       MD_PAGE.ISDELETED = 0
    AND         (@ISACTIVE = 2 OR MD_PAGE.ISACTIVE = @ISACTIVE)
    AND         (@FROMDATE IS NULL OR MD_PAGE.CREATEDDATE >= @FROMDATE)
    AND         (@TODATE IS NULL OR MD_PAGE.CREATEDDATE < @TODATE + 1)
    AND         (
                    @KEYWORD IS NULL 
                    OR @KEYWORD = ''
                    OR (MD_PAGE.TITLEPAGE like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
                    OR (MD_PAGE.[PAGENAME] like '%'+@KEYWORD+'%'  collate Latin1_General_CI_AI_WS)
		        )   
    
    ORDER BY    MD_PAGE.CREATEDDATE DESC   
	OFFSET      (@PAGEINDEX -1)* @PAGESIZE ROWS
	FETCH NEXT  @PAGESIZE ROWS ONLY

END
GO
